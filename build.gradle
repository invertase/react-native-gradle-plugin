plugins {
  id 'java'
  id 'groovy'
  id 'maven'
  id 'com.gradle.plugin-publish' version '0.12.0'
}

group 'io.invertase.gradle'
version = System.getenv('PLUGIN_VERSION') ?: '1.4'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
  mavenCentral()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: uri('repo'))
    }
  }
}

pluginBundle {
  website = 'https://github.com/invertase/react-native-gradle-plugin'
  vcsUrl = 'https://github.com/invertase/react-native-gradle-plugin'
  description = 'React Native Plugin'
  tags = ['react-native', 'react', 'firebase', 'invertase']

  plugins {
    reactNativeBuildPlugin {
      id = 'io.invertase.gradle.build'
      displayName = 'React Native Build Plugin'
    }
    reactNativeSettingsPlugin {
      id = 'io.invertase.gradle.settings'
      displayName = 'React Native Settings Plugin'
    }
  }
}

task setupPluginUpload << {
  def key = System.getenv('GRADLE_PUBLISH_KEY')
  def secret = System.getenv('GRADLE_PUBLISH_SECRET')

  if (!key || !secret) {
    throw new RuntimeException("GRADLE_PUBLISH_KEY and/or GRADLE_PUBLISH_SECRET are not defined environment variables")
  }

  System.properties.setProperty("gradle.publish.key", key)
  System.properties.setProperty("gradle.publish.secret", secret)
}

tasks.publishPlugins.dependsOn tasks.setupPluginUpload
